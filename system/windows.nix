{ pkgs, ... }: {
  services.yabai =
    let
      package = pkgs.yabai;
      yabai = "${package}/bin/yabai";
      xargs = "xargs";
      jq = "${pkgs.jq}/bin/jq";
    in
    {
      inherit package;
      enable = true;
      enableScriptingAddition = true;
      config = {
        layout = "bsp";
        split_ratio = "0.5";
        split_type = "auto";
        auto_balance = "off";
        window_zoom_persist = "on";
        window_placement = "second_child";
        top_padding = "15";
        bottom_padding = "15";
        left_padding = "15";
        right_padding = "15";
        window_gap = "10";
        window_shadow = "off";
        window_opacity = "on";
        window_opacity_duration = "0.0";
        active_window_opacity = "1.0";
        normal_window_opacity = "0.8";
        mouse_drop_action = "swap";
        mouse_modifier = "shift";
        mouse_action1 = "move";
        mouse_action2 = "resize";
      };
      extraConfig = ''
        ${yabai} -m rule --add app="^Pika$" manage=off
        ${yabai} -m rule --add app="^Stats$" manage=off
        ${yabai} -m rule --add app="^Calculator$" manage=off
        ${yabai} -m rule --add app="^System Settings$" manage=off
        ${yabai} -m rule --add app="^Archive Utility$" manage=off
        ${yabai} -m rule --add app="^Software Update$" manage=off
        ${yabai} -m rule --add label="Finder" app="^Finder$" title="(Copy|Connect|Move|Info|Pref)" manage=off

        ${yabai} -m signal --add event=space_created action='
          prev_space=$(${yabai} -m query --spaces --space)
          new_space=$(${yabai} -m query --spaces --space "$YABAI_SPACE_INDEX")
          space_id=$(${jq} -e "select(.\"is-native-fullscreen\") | .id" <<< "$new_space") || exit
          json_space=$(${jq} "{ id: $space_id, homeSpace: .id } | tojson" <<< "$prev_space")
          label=$(${jq} -Rr "ltrimstr(\"\\\"\") | rtrimstr(\"\\\"\")" <<< "$json_space")
          ${yabai} -m space "$YABAI_SPACE_INDEX" --label "$label"
        '

        ${yabai} -m signal --add event=space_changed action='
          home_spaces=$(${yabai} -m query --spaces | ${jq} "map(.label | try fromjson | .homeSpace // empty)")
          ${yabai} -m query --spaces \
          | ${jq} "
              map(
                select(.windows | length == 0)
                | select(.\"has-focus\" | not)
                | select(.id as \$x | $home_spaces | index(\$x) | not)
              )
              | reverse
              | .[]
              | .index
          " \
          | ${xargs} -I{} ${yabai} -m space --destroy {}
        '
      '';
    };
}
